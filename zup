#!/bin/zsh
#set -eux -o pipefail

autoload -U add-zsh-hook

function run {
    (
        source .venv/bin/activate
        export PYTHONPATH=python
        python $@
    )
}

function before {
    local command=${1[0,-2]}
    if [[ $command != '' ]]; then
        osh_current_command=(
            $(date +%s)  # start time
            $command  # command
        )
    fi
}

function after {
    local exit_code=$?
    if [[ -v osh_current_command ]]; then
        osh_session=${osh_session:-$(uuidgen)}
        osh_current_command+=(
            $(date +%s)  # end time
            $exit_code  # exit code
            $(pwd)  # folder
            $(hostname)  # machine
            $osh_session  # session
        )
        run -m osh.socket insert-event $osh_current_command &!
        unset osh_current_command
    fi
}

add-zsh-hook zshaddhistory before
add-zsh-hook precmd after

function osh-search {
    BUFFER=$(run -m osh.socket fzf-select)
    CURSOR=$#BUFFER
    zle reset-prompt
}

zle -N osh-search
bindkey '^e' osh-search
